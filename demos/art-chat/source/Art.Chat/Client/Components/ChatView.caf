import &ArtSuite

class ChatView extends FluxComponent
  @subscriptions
    history:
      model:  :chatsByChatRoom
      key:    ({chatRoom}) ->  chatRoom

  @stateField message:  null
  @propFields chatRoom: :artSuite
  @getter
    user:            -> @props.currentUser
    messagePostable: -> present @message

  postMessage: ->
    @models.chat.create data: {}
      @user
      @message
      @chatRoom

    # slight delay - sometimes a "\n" is added AFTER this when "enter" is pressed
    @refs.messageInput.element.value = ""
    timeout 0 -> @refs.messageInput.element.value = ""

  valueChanged: (event) -> @message = event.props.value

  componentWillMount: ->
    timeout 500 -> @refs.messageInput.element.focus()
    super

  render: ->
    {gridSize} = &StyleProps

    Element
      :column
      draw: &StyleProps.chatViewBackground

      Element
        :row :childrenCenterLeft
        size:             h: gridSize * 3
        padding:          h: gridSize / 2
        childrenMargins:  gridSize / 2
        draw:             &Palette.primary
        &Button           text: :â–¼ action: @props.exit

        TextElement
          :childrenSize
          &StyleProps.titleText
          text: @props.currentUser

      ScrollElement
        clip:   true
        track:  :bottom

        Element
          padding:  10
          :column :parentWidthChildrenHeight

          array postMessage from @history
            &ChatMessage
              postMessage
              {} @props.currentUser

      Element
        :row :childrenCenterLeft
        :childrenHeightParentWidth
        padding: right: gridSize / 2
        draw:
          outline:    &Palette.black.quandary
          &Palette.gray

        Element
          :parentWidthChildrenHeight
          padding:  h: 7
          draw:
            padding:    gridSize / 2
            rectangle:  radius: gridSize
            &Palette.white.primary
            outline:    &Palette.black.quandary

          TextInputElement
            &StyleProps.mediumText
            key:          :messageInput
            on:           enter: @postMessage, valueChanged: @valueChanged
            type:         :textarea
            size:         ww:1 hch:1 max: h: 160
            padding:      h: 10 v: gridSize
            placeholder:  "" new message
        &Button text: :send color: true action: @messagePostable && @postMessage