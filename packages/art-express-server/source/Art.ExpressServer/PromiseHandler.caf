import &ArtStandardLib, &ArtCommunicationStatus, &ArtClassSystem
querystring = &querystring

class PromiseHandler extends &LoggingMixin BaseClass

  @getMiddleware: (options) ->
    new @ options
    .middleware

  constructor: (@options = {}) ->
    super
    @logVerbose :initialized # : {@options}
    @_commonResponseHeaders = @options.commonResponseHeaders

  ####################
  # OVERRIDABLES
  ####################
  # set handleUrlRegex to match the Urls you want to handle
  @getter handleUrlRegex: -> null

  # override for more control over which requests you handle
  canHandleRequest: (request) ->
    if @handleUrlRegex
      @handleUrlRegex.test request.url
    else
      true

  handleApiRequest:   null
  handleHtmlRequest:  null  # (request, jsonData) -> promise.then (htmlString) ->

  handleRequest: (request, requestData) ->
    ##
      IN:
        request: the express/node request object
        requestData: the entire request data, as a string, otherwise unprocessed
      OUT:
        promise.then (plainResponseObject) ->

        plainResponseObject:
          headers:    plainHeadersObject
          data:       (optional) string
          statusCode: Http Status-code
          status:     Art.CommunicationStatus

        if plainResponseObject is null, a 'missing' or '404' status is returned.

        for redirects:
          statusCode: 3xx
          data:       URL to redirect to

      OVERRIDE this for handling all requests

    Promise.then ->

      # TODO - eventually this should check the Content-Type, too
      switch
      when @handleApiRequest
        @_handleApiRequestWrapper request, requestData

      when @handleHtmlRequest
        @_handleHtmlRequestWrapper request, requestData

      else null

  ######################
  # ExpressHandler API
  ######################

  ## gets called after ganHandleRequest passes
  handleRawRequest: (request, response, next) ->
    dataChunks = []
    requestData = null
    request.on :data (chunk) -> dataChunks.push chunk

    request.on :end ->
      Promise.then ->
        @handleRequest request, requestData = dataChunks.join ''

      .catch (error) ->
        @logError internalError: {request, error}
        status:       serverFailure

      .then (plainResponse) ->

        if plainResponse
          {headers, data, status, statusCode} = plainResponse
          unless data? || status? || statusCode?
            throw new Error "expected data, status or statusCode in response: #{formattedInspect plainResponse}"

        responseData = data

        response.statusCode = statusCode || if status && statusCode = encodeHttpStatus status
          statusCode
        else
          statusCode = if data then 200 else 404


        if (statusCode/100 | 0) == 5
          @logError
            url:              request.url
            requestData:      (try JSON.parse requestData ) || requestData
            responseData:     (try JSON.parse responseData) || responseData

        if (statusCode/100 | 0) == 3
          response.redirect statusCode, data
        else
          @_encodeOutput request, response, headers, responseData

  @getter middleware: ->
    (request, response, next) ->
      if @canHandleRequest request
        @logVerbose start: {request.method, request.url}

        @handleRawRequest request, response, next

      else
        # @logVerbose {} notHandled: request.url, @handleUrlRegex

        next()

  ###################
  # PRIVATE
  ###################
  _encodeJson: (responseHeaders, responseData) ->
    responseHeaders[:Content-Type] = "" application/json; charset=UTF-8
    JSON.stringify responseData

  _encodeHtml: (responseHeaders, responseData) ->
    responseHeaders[:Content-Type] = "" text/html; charset=UTF-8
    if isString responseData
      responseData
    else
      """
        <html><body style='font-family:Monaco,courier;font-size:10pt'>
        #{
          formattedInspect responseData
          .replace /\n/g, "<br>\n"
          .replace /\ /g, "&nbsp;"
          .replace
            ///g (#{findUrlRegexp.source})
            "<a href='$1'>$1</a>"
        }
        </body></html>

  _encodePlain: (responseHeaders, responseData) ->
    responseHeaders[:Content-Type] = "" text/plain; charset=UTF-8
    if isString responseData
      responseData
    else
      formattedInspect responseData

  _encodeOutput: (request, response, responseHeaders = {}, responseData) ->
    {accept = 'text/html'} = request.headers

    encodedData = responseData && switch
    when /json/.test accept then @_encodeJson  responseHeaders, responseData
    when /html/.test accept then @_encodeHtml  responseHeaders, responseData
    else                         @_encodePlain responseHeaders, responseData

    each v, k from headers = merge @_commonResponseHeaders, responseHeaders
      response.setHeader k, v

    @logVerbose done: {request.method, request.url, accept, responseData, headers, encodedData}
    response.end encodedData

  _handleHtmlRequestWrapper: (request, requestData) ->
    Promise.then ->
      @handleHtmlRequest request, requestData
    .then (data) ->
      switch
      when !data? then status: missing
      when isPlainObject data then data
      when isString(data) || data = data.toString?()
        {data}
      else
        throw new Error "ArtExpressServer.PromiseHandler#_handleHtmlRequestWrapper - expected string, plainObject, object with toString() or null response"

  _handleApiRequestWrapper: (request, requestData) ->

    Promise.then -> JSON.parse requestData || "{}"
    .catch -> throw new Error "" requested data was not valid JSON: #{requestData}
    .then (parsedData) ->
      {url} = request
      [__, query] = url.split :?
      merge
        parsedData
        query && object v from querystring.parse query
          try
            JSON.parse v
          catch
            v

    .then (parsedData) ->
      @handleApiRequest request, parsedData

    .then (data) ->
      if data then {data} else status: missing
