import &StandardImport

subscriptionEstablishmentTimeout = 25

suite:
  "basic requests": ->
    setup ->
      Config.onConnected()

    test "create should only notify related queries", ->
      pipelines.simpleStore.create data: noodleId: :noodle1

    test "update should notify related queries and the updated record", ->
      pipelines.simpleStore.create                  data: noodleId: :noodle1
      .then ({id}) -> pipelines.simpleStore.update  data: noodleId: :noodle2 id: id

    test "delete should notify related queries and the deleted record", ->
      pipelines.simpleStore.create data: noodleId: :noodle1
      .then ({id}) -> pipelines.simpleStore.delete key: id

  "artEryPusherSession": ->
    setup -> session.reset()

    test "request generates session", ->
      assert.doesNotExist session.data.artEryPusherSession
      pipelines.simpleStore.create data: noodleId: "noodle1"
      .then -> assert.isString session.data.artEryPusherSession

    test "persists across requests", ->
      assert.doesNotExist session.data.artEryPusherSession
      pipelines.simpleStore.create data: noodleId: :noodle1
      .then ->
        assert.isString session.data extract artEryPusherSession
        pipelines.simpleStore.create data: noodleId: :noodle2
        .then -> assert.eq artEryPusherSession, session.data.artEryPusherSession

  "round trip tests": ->
    subscribeTest = ({data, requestType, subscriptionPipeline, subscriptionKey}) ->
      subscriptionPipeline ?= :simpleStore
      test "#{subscriptionPipeline}.#{requestType} should trigger event" ->
        pipelines.simpleStore.create()
        .then ({id}) ->
          subscriptionKey ?= id
          subscription = null

          new Promise (_resolve) ->
            subscription = subscribeToChanges subscriptionPipeline, subscriptionKey, _resolve

            pipelines.simpleStore[requestType] {} data, key: id

          .then subscription.unsubscribe

    setup -> Config.onConnected()

    subscribeTest requestType: :update data: foo: :bar
    subscribeTest requestType: :delete

    subscribeTest
      requestType:          :update
      data: noodleId:       :123
      subscriptionKey:      :123
      subscriptionPipeline: :pusherTestsByNoodleId
