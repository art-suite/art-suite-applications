import &StandardImport

&ArtConfig.configure()

describe
  basic: ->
    test :before ->
      class MyPipeline extends Pipeline
        @handlers helloWorld: ({data}) -> "Hello #{data}."
        @publicRequestTypes :helloWorld

        @filter
          before: helloWorld: (request) ->
            request.withData upperCamelCase request.data

      pipelines.myPipeline.helloWorld data: "frank"
      .then (result) ->
        assert.eq result, "Hello Frank."

    test :all ->
      class MyPipeline extends Pipeline
        @handlers helloWorld: ({data}) -> "Hello #{data}."
        @publicRequestTypes :helloWorld

        @filter
          before: all: (request) ->
            request.withData upperCamelCase request.data

      pipelines.myPipeline.helloWorld data: "frank"
      .then (result) ->
        assert.eq result, "Hello Frank."


    test :after ->
      class MyPipeline extends Pipeline
        @handlers helloWorld: ({data}) -> "Hello #{data}."
        @publicRequestTypes :helloWorld

        @filter
          after: helloWorld: (request) ->
            request.withData request.data + " How are you?"

      pipelines.myPipeline.helloWorld data: "frank"
      .then (result) ->
        assert.eq result, "Hello frank. How are you?"

  order:
    basic: ->
      test :two ->
        class MyPipeline extends Pipeline
          @handlers helloWorld: ({data}) -> "Hello #{data}."
          @publicRequestTypes :helloWorld

          @filter
            before: helloWorld: (r) -> r.withData r.data + :A
            after:  helloWorld: (r) -> r.withData r.data + :A

          @filter
            before: helloWorld: (r) -> r.withData r.data + :B
            after:  helloWorld: (r) -> r.withData r.data + :B

        pipelines.myPipeline.helloWorld data: "frank"
        .then (result) ->
          assert.eq result, "Hello frankBA.AB"

    groups: ->
      test :grouped ->
        class MyPipeline extends Pipeline
          @handlers myAction: -> :-
          @publicRequestTypes :myAction

          @filter name: :filterA  group: :outer    after: myAction: (r) -> r.withData "" #{r.data}A
          @filter name: :filterB  group: :inner     after: myAction: (r) -> r.withData "" #{r.data}B
          @filter name: :filterC  group: :middle    after: myAction: (r) -> r.withData "" #{r.data}C
          @filter name: :filterD  group: :loggers   after: myAction: (r) -> r.withData "" #{r.data}D

        pipelines.myPipeline.myAction()
        .then (result) ->
          assert.eq result, "-BCAD"

      test :ungrouped ->
        class MyPipeline extends Pipeline
          @handlers myAction: -> :-
          @publicRequestTypes :myAction

          @filter name: :filterA  after: myAction: (r) -> r.withData "" #{r.data}A
          @filter name: :filterB  after: myAction: (r) -> r.withData "" #{r.data}B
          @filter name: :filterC  after: myAction: (r) -> r.withData "" #{r.data}C
          @filter name: :filterD  after: myAction: (r) -> r.withData "" #{r.data}D

        pipelines.myPipeline.myAction()
        .then (result) ->
          assert.eq result, "-ABCD"

  query: ->
    test "query handlers with after-all filter" ->
      class Post extends Pipeline
        @query postByUserId: (request) -> [] request.key, 1, 2, 3
        @publicRequestTypes :postByUserId

        @filter
          after: all: (response) ->
            response.withData array a from response.data with "#{a} #{a}"

      assert.eq Post.post.clientApiMethodList, [] "postByUserId"
      Post.post.postByUserId key: :foo
      .then (results) ->
        assert.eq results, [] "foo foo" "1 1" "2 2" "3 3"

  filterLogs: ->
    test "basic", ->
      class MyPipeline extends Pipeline
        @handlers foo: (request) -> merge request.data, myHandlerRan: true
        @publicRequestTypes :foo

        @filter
          name: "MyBeforeFooFilter"
          before: foo: (request) -> request.withMergedData myBeforeFooFilterRan: true

        @filter
          name: "MyAfterFooFilter"
          after: foo: (response) -> response.withMergedData myAfterFooFilterRan: true

      p = new MyPipeline
      p.foo returnResponseObject: true
      .then (response) ->
        assert.eq [:created :MyBeforeFooFilter, :foo-handler], array {name} in response.beforeFilterLog with name
        assert.eq [:MyAfterFooFilter               ], array {name} in response.afterFilterLog  with name
        assert.eq
          response.data
          myHandlerRan: true
          myBeforeFooFilterRan: true
          myAfterFooFilterRan: true

  location: ->
    test "before filters by location", ->
      class MyPipeline extends Pipeline
        @handlers foo: (request) -> merge request.data, myHandlerRan: true
        @publicRequestTypes :foo

        @filter
          location: "client"
          name: "beforeFooClient"
          before: foo: (request) -> request

        @filter
          location: "both"
          name: "beforeFooBoth"
          before: foo: (request) -> request

        @filter
          location: "server"
          name: "beforeFooServer"
          before: foo: (request) -> request

      assert.eq ["client", "both"],           array f in pipelines.myPipeline.getBeforeFilters requestType: "foo", location: "client" with f.location
      assert.eq ["server", "both"],           array f in pipelines.myPipeline.getBeforeFilters requestType: "foo", location: "server" with f.location
      assert.eq ["client", "server", "both"], array f in pipelines.myPipeline.getBeforeFilters requestType: "foo", location: "both"   with f.location

    test "after filters by location", ->
      class MyPipeline extends Pipeline
        @handlers foo: (request) -> merge request.data, myHandlerRan: true
        @publicRequestTypes :foo

        @filter
          location: "client"
          name: "afterFooClient"
          after: foo: (request) -> request

        @filter
          location: "both"
          name: "afterFooBoth"
          after: foo: (request) -> request

        @filter
          location: "server"
          name: "afterFooServer"
          after: foo: (request) -> request

      assert.eq ["both", "client"],           array f in pipelines.myPipeline.getAfterFilters requestType: "foo", location: "client" with f.location
      assert.eq ["both", "server"],           array f in pipelines.myPipeline.getAfterFilters requestType: "foo", location: "server" with f.location
      assert.eq ["both", "server", "client"], array f in pipelines.myPipeline.getAfterFilters requestType: "foo", location: "both"   with f.location


  filterFailures: ->
    test :basic ->
      class MyPipeline extends Pipeline
        @handlers foo: (request) -> throw new Error "simulated error on foo request"
        @publicRequestTypes :foo

        @filter
          name: :FilterWithoutFilterFailures
          # filterFailures: true
          after: foo: (response) ->
            response.success data: "not me"

        @filter
          name: :FilterWithFilterFailures
          filterFailures: true
          after: foo: (response) ->
            # log FilterWithFilterFailures: response
            response.success data: "me!"


      assert.eq false, pipelines.myPipeline.filters[0].filterFailures
      assert.eq true, pipelines.myPipeline.filters[1].filterFailures

      pipelines.myPipeline.foo()
      .then (result)->
        assert.eq result, "me!"

  groups: ->


  regressions: ->
    test "can share filter instances" ->
      class MyFilter extends Filter
        @after foo: (response) ->
          response.withMergedData myFiltered: true

      instance = new MyFilter

      class MyPipeline1 extends Pipeline
        @publicRequestTypes :foo
        @handlers foo: (request) -> foo1ed: true
        @filter instance

      class MyPipeline2 extends Pipeline
        @publicRequestTypes :foo
        @handlers foo: (request) -> foo2ed: true
        @filter instance

      Promise.all([
        pipelines.myPipeline1.foo()
        pipelines.myPipeline2.foo()
      ]).then ([foo1, foo2]) ->
        assert.eq foo1, foo1ed: true, myFiltered: true
        assert.eq foo2, foo2ed: true, myFiltered: true